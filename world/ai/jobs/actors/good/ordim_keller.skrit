/*
  ============================================================================
  ----------------------------------------------------------------------------

  File    :   job_talk_edgaar.skrit

  Author(s)  :   David Tomandl, Eric Tams, Chad Queen, Bartosz Kijanka

  Purpose    :   Custom talk job for edgaar, for the clean out his basement quest.
          Now with custom animations!

  (C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go    m_Go$;
GoMind  m_Mind$;
GoBody  m_Body$;
Job    m_Job$;
Goid  m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//  init
event OnJobInitPointers$( Job job$ )
{
  m_Job$    = job$;
  m_Go$    = job$.Go;
  m_Mind$    = job$.Go.Mind;
  m_Body$    = job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
  OnJobInitPointers$( job$ );

  m_Target$  = job$.GoalObject;

  SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//  blab blab away... pretend you're Lee on coffee
state Talk$
{
  transition
  {
    -> Exiting$:  OnWorldMessage( WE_REQ_TALK_END ) =
    {
      if( m_Go$.Mind.ActorAutoFidgets )
      {
        StartFidgetIfRequired$();
      }
    }
  }

  trigger OnWorldMessage$( WE_ANIM_DONE )
  {
    TryAnim$('edg2');
  }

  event OnEnterState$
  {
    if ( GameAuditor.GetDb.GetBool( "krug_in_cellar_dead" ) )
    {
            m_Go$.GetConversation.RSSelectConversation( "conv_ordim_2" );
                DumpMain$;
    } else {
      m_Go$.GetConversation.RSSelectConversation( "conv_ordim" );
    }

    if ( GameAuditor.GetDb.GetBool( "talked_with_edgaar_once" ) )
    {
      if ( GameAuditor.GetDb.GetBool( "talked_with_edgaar_twice" ) )
      {
        TryAnim$('edg2');
      }
      else
      {
        TryAnim$('edg1'); // second time you talk to edgaar, he uses a special animation
        GameAuditor.GetDb.SetBool( "talked_with_edgaar_twice", true );
      }

    }
    else
    {
      GameAuditor.GetDb.SetBool( "talked_with_edgaar_once", true );
      TryAnim$('edg2');
    }

    m_Go$.GetConversation.RSActivateDialogue();
  }
}


////////////////////////////////////////////////////////////////////////////////
//  finish blabbing
state Exiting$
{
  event OnEnterState$
  {
    m_Job$.MarkForDeletion();
  }
}